buildscript {
    ext.kotlin_version = '1.2.51'
    ext.cockpit_core_version = '2.0.0'
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.gradle.publish:plugin-publish-plugin:0.9.10"
        classpath files("../CockpitCore/build/libs/CockpitCore-${cockpit_core_version}.jar")
    }
}

plugins {
    id 'groovy'
    id 'maven'
    id "com.gradle.plugin-publish" version "0.9.10"
    id 'org.jetbrains.kotlin.jvm' version '1.2.51'
    id 'java-gradle-plugin'
}

group = 'com.polidea.cockpit'
version = '2.0.0'

sourceCompatibility = 1.7

repositories {
    google()
    jcenter()
    mavenCentral()
}

configurations {
    provided
}

sourceSets {
    main {
        compileClasspath += configurations.provided
    }

    test.java.srcDirs += 'src/test/kotlin'
    main.java.srcDirs += project(':CockpitCore').file('src/main/java')
}

jar {
    from zipTree("../CockpitCore/build/libs/CockpitCore-${cockpit_core_version}.jar")
}

dependencies {
    implementation gradleApi()
    implementation localGroovy()
    provided 'com.android.tools.build:gradle:3.1.3'
    implementation 'org.yaml:snakeyaml:1.20'
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    implementation 'com.squareup:javapoet:1.10.0'

    testImplementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
    compile files("../CockpitCore/build/libs/CockpitCore-${cockpit_core_version}.jar")
}

pluginBundle {
    website = 'https://github.com/Polidea/Cockpit'
    vcsUrl = 'https://github.com/Polidea/Cockpit'
    description = 'Gradle plugin for Android Cockpit library.'
    tags = ['gradle', 'plugin', 'android']

    plugins {
        cockpitPlugin {
            id = 'com.polidea.cockpit'
            displayName = 'Gradle Cockpit plugin'
        }
    }
}

install {
    repositories.mavenInstaller {
        pom.version = project.version
        pom.artifactId = 'cockpit'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = "4.6"
}